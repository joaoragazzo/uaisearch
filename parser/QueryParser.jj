options {
    STATIC = false;
    IGNORE_CASE = false;
    LOOKAHEAD = 3;  // Aumentado para 3 para melhor detecção de padrões
}

PARSER_BEGIN(QueryParser)
package com.elastic.aisearch.parser;

import java.io.StringReader;
import java.util.*;
import lombok.Getter;

public class QueryParser {

    @Getter
    public class QueryNode {
        private String shouldContent = "";

        private List<String> mustNotContent = new ArrayList<>();
        private List<String> mustInContent = new ArrayList<>();

        private List<String> mustTitle = new ArrayList<>();
        private List<String> mustNotTitle = new ArrayList<>();

        private String minDate = "";
        private String maxDate = "";
        private String eqDate = "";

        private String minReadingTime = "";
        private String maxReadingTime = "";
        private String eqReadingTime = "";

        @Override
        public String toString() {
            StringBuilder sb = new StringBuilder();
            sb.append("<b>shouldContent:</b> ").append(shouldContent).append("</br>");

            sb.append("<b>mustNotContent:</b> </br>");
            for (String s : mustNotContent) {
                sb.append("  - ").append(s).append("</br>");
            }

            sb.append("<b>mustInContent:</b> </br>");
            for (String s : mustInContent) {
                sb.append("  - ").append(s).append("</br>");
            }
            
            sb.append("</br>");

            sb.append("<b>mustTitle:</b> ").append("</br>");
            for (String s : mustTitle) {
                sb.append("  - ").append(s).append("</br>");
            }
            sb.append("<b>mustNotTitle:</b> ")  .append("</br>");
            for (String s : mustNotTitle) {
                sb.append("  - ").append(s).append("</br>");
            }

            sb.append("</br>");

            sb.append("<b>minDate:</b> ").append(minDate).append("</br>");
            sb.append("<b>maxDate:</b> ").append(maxDate).append("</br>");
            sb.append("<b>eqDate:</b> ").append(eqDate).append("</br>");

            sb.append("</br>");

            sb.append("<b>minReadingTime:</b> ").append(minReadingTime).append("</br>");
            sb.append("<b>maxReadingTime:</b> ").append(maxReadingTime).append("</br>");
            sb.append("<b>eqReadingTime:</b> ").append(eqReadingTime).append("</br>");

            return sb.toString();
        }

    }

    public QueryNode node = new QueryNode();

    public QueryNode parseQuery(String query) throws ParseException {
        ReInit(new StringReader(query));
        Start();
        return node;
    }
}
PARSER_END(QueryParser)

SKIP : {
      " " 
    | "\t" 
    | "\r" 
    | "\n"
}

// Tokens de filtros têm prioridade sobre palavras
TOKEN : {
      < OR: "OR" >
    | < READING_TIME: "reading_time:" >
    | < CREATED_AT: "created_at:" >
    | < IN_CONTENT: "in_content:" >
    | < IN_TITLE: "in_title:" >
    | < NOT_IN_TITLE: "-in_title:" >
    | < GT: ">" >
    | < LT: "<" >
    | < RANGE_DOTS: ".." >
    | < NEGATED_QUOTED: "-" "\"" (~["\""])* "\"" >
    | < DATE: (["0"-"9"]){2} "/" (["0"-"9"]){2} "/" (["0"-"9"]){4} >
    | < NUMBER: (["0"-"9"])+ >
    | < QUOTED: "\"" (~["\""])* "\"" >
    | < NEGATED_WORD: "-" (~[" ", "\t", "\n", "\r"])+ >
    | < WORD: (~[" ", "\t", "\n", "\r"])+ >
}

void Start() : { Token word, quoted, t; }
{
    (
    LOOKAHEAD(3)
    <READING_TIME> ReadingTimeFilter()
    | LOOKAHEAD(3)
    <CREATED_AT> DateFilter()
    | LOOKAHEAD(3)
    <IN_TITLE> InTitleFilter(true)
    | LOOKAHEAD(3)
    <NOT_IN_TITLE> InTitleFilter(false)
    | LOOKAHEAD(3)
    <IN_CONTENT> InContentFilter()
    | LOOKAHEAD(2)
    quoted = < QUOTED > 
        {
            String content = quoted.image.substring(1, quoted.image.length() - 1);
            this.node.mustInContent.add("\"" + content + "\"");
        }
    | LOOKAHEAD(2)
    word = < NUMBER > 
        {
            this.node.shouldContent += word.image + " ";
        }
    | LOOKAHEAD(2)
    word = < NEGATED_WORD >
        {
            this.node.mustNotContent.add(word.image.substring(1));
        }
    | LOOKAHEAD(2)
    word = < NEGATED_QUOTED >
        {
            String content = word.image.substring(2, word.image.length() - 1);
            this.node.mustNotContent.add("\"" + content + "\"");
        }
    | word = < WORD > 
        {
            this.node.shouldContent += word.image + " ";
        }
    )+
}

void DateFilter() : { Token d1, d2; }
{
    (
            <GT> d1 = <DATE> { this.node.minDate = d1.image; }
       |    <LT> d1 = <DATE> { this.node.maxDate = d1.image; }
       | 
            d1 = <DATE>
            [
                <RANGE_DOTS> d2 = <DATE> {
                    this.node.minDate = d1.image;
                    this.node.maxDate = d2.image;
                }
            ]
            {
                if(this.node.minDate.equals("")) { 
                    this.node.eqDate = d1.image;
                }
            }
    )
}

void ReadingTimeFilter() : { Token t1, t2; }
{
    (
            <GT> t1 = <NUMBER> { this.node.minReadingTime = t1.image; }
       |    <LT> t1 = <NUMBER> { this.node.maxReadingTime = t1.image; }
       | 
            t1 = <NUMBER>
            [
                <RANGE_DOTS> t2 = <NUMBER> {
                    this.node.minReadingTime = t1.image;
                    this.node.maxReadingTime = t2.image;
                }
            ]
            {
                if(this.node.minReadingTime.equals("")) { 
                    this.node.eqReadingTime = t1.image;
                }
            }
    )
}

void InTitleFilter(boolean must) : { Token t1; }
{
        t1 = <WORD>
        {
            if (must)
                this.node.mustTitle.add(t1.image);
            else
                this.node.mustNotTitle.add(t1.image);            
        }
    |   t1 = <QUOTED>
        {
            String content = t1.image.substring(1, t1.image.length() - 1);
            if (must)
                this.node.mustTitle.add("\"" + content + "\"");
            else 
                this.node.mustNotTitle.add("\"" + content + "\"");
        }
}

void InContentFilter() : { Token t1; }
{
        t1 = <WORD>
        {
            this.node.mustInContent.add(t1.image);           
        }
    |   t1 = <QUOTED>
        {
            String content = t1.image.substring(1, t1.image.length() - 1);
            this.node.mustInContent.add("\"" + content + "\"");
        }
}